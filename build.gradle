import dependancies.Config
import dependancies.Deps

buildscript {
    ext.kotlin_version = '1.3.61'

    repositories {
        google()
        jcenter()
        gradlePluginPortal()
    }

    dependencies {
        classpath Deps.Tools.androidStudio
        classpath Deps.Tools.kotlinGradle
        classpath Deps.Tools.gradleVersions
        classpath Deps.Tools.jacocoCore
        classpath Deps.Tools.dicemelonJacoco
        classpath Deps.Tools.jacocoPalantir
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            apply plugin: "com.github.ben-manes.versions"

            //<editor-fold desc="android common DSL">
            android {
                flavorDimensions "branding"
                defaultPublishConfig "ncgRelease"

                productFlavors {
                    ncg {
                        dimension "branding"
                        resValue "string", "application_name", "Jataayu NCG"
                        buildConfigField "boolean", "is_ncg", "true"
//                        applicationId "com.ncg"
                    }
                    nhs {
                        dimension "branding"
                        resValue "string", "application_name", "Jataayu NHS"
                        buildConfigField "boolean", "is_ncg", "false"
//                        applicationId "com.nhs"
                    }
                }

                buildToolsVersion Config.buildToolsVersion
                compileSdkVersion Config.compileSdkVersion

                defaultConfig {
                    minSdkVersion Config.minSdkVersion
                    targetSdkVersion Config.compileSdkVersion

                    testInstrumentationRunner "in.projecteka.jataayu.testUtil.JataayuJUnitRunner"
                    resValue "string", "cm_config_provider", "@ncg"
                }

                compileOptions {
                    sourceCompatibility Config.javaVersion
                    targetCompatibility Config.javaVersion
                }

                signingConfigs {
                    release {
                        storeFile file(String.valueOf(System.getenv("UPLOAD_KEY_PATH")))
                        storePassword System.getenv("UPLOAD_KEY_PASSWORD")
                        keyAlias System.getenv("UPLOAD_KEY")
                        keyPassword System.getenv("UPLOAD_KEY_PASSWORD")
                    }
                }

                buildTypes {
                    debug {
                        testCoverageEnabled true
                        debuggable true
                        minifyEnabled false
                    }
                    qa {
                        //fix app routing on minification
                        debuggable false
                        minifyEnabled false
                        signingConfig = signingConfigs.release
                    }
                    release {
                        //fix app routing on minification
                        testCoverageEnabled false
                        debuggable false
                        minifyEnabled false
                        signingConfig = signingConfigs.release
                    }
                }

                dataBinding {
                    enabled = true
                    enabledForTests = true
                }

                kotlin {
                    experimental {
                        coroutines "enable"
                    }
                }

                lintOptions {
                    warning 'InvalidPackage'
                }

                testOptions {
                    unitTests.returnDefaultValues = true
                    animationsDisabled = true
                }
            }

            kapt {
                correctErrorTypes = true
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
